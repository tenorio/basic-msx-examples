10 REM MEDIA ESCOLAR
20 SCREEN0
30 LOCATE 0,0
40 INPUT"QUANTOS ALUNOS";NA
50 BEEP:PRINT"CONFIRMA? (S/N)"
60 A$=INPUT$(1)
70 IF A$="S" OR A$="s" THEN 90
80 GOTO 30
90 DIM NO(NA),X2(NA),NC(NA)
100 PRINT
110 PRINT"DIGITE AS NOTAS DE CADA ALUNO"
120 PRINT"SE VOCç ERRAR ANTES DE TECLAR <ENTER>, APAGUE COM <BS> E DIGITE NOVAMENTE"
130 PRINT"SE VOCç ERRAR DEPOIS DE TECLAR <ENTER>, MARQUE O NãMERO DO ALUNO E CORRIJA AO FINAL DO PROCESSO"
140 PRINT
150 FOR I=1 TO NA
160 PRINT"ALUNO NR";I,:INPUT "NOTA=";NO(I)
170 NEXT I
180 CLS
190 FOR I=1 TO NA
200 PRINT"ALUNO NR";I,"NOTA=";NO(I)
210 NEXT I
220 BEEP:PRINT"ALGUM ERRO? (S/N)"
230 A$=INPUT$(1)
240 IF A$="N" OR A$="n" THEN 280
250 INPUT"QUAL O NR DO ALUNO";K
260 INPUT"QUAL A NOTA CORRETA";NC
270 NO(K)=NC:GOTO 180
280 TL=0
290 FOR I=1 TO NA
300 TL=TL+NO(I)
310 NEXT I
320 MEDIA=TL/NA
330 SOMA=0
340 FOR I=1 TO NA
350 X=NO(I)-MEDIA
360 X2=X*X
370 SOMA=SOMA+X2
380 NEXT I
390 DESVIO=SQR(SOMA/NA)
400 CLS
410 PRINT"MEDIA (X)=";
420 PRINT USING "##.##";MEDIA
430 PRINT"DESVIO PADRAO ( )=";
440 PRINT USING "##.###";DESVIO
450 PRINT"ALUNO  NOTA REAL   NOTA CORRIGIDA"
460 FOR I=1 TO NA
470 IF DESVIO = 0 THEN NC(I)=5:GOTO 510
480 NC(I)=((NO(I)-MEDIA)/DESVIO)*5/3+5
490 IF NC(I)<0 THEN NC(I)=0
500 IF NC(I)>10 THEN NC(I)=10
510 PRINT TAB(1):PRINT USING "##";I;
520 PRINT TAB(8):PRINT USING "##.##";NO(I);
530 PRINT TAB(22):PRINT USING "##.##";NC(I)
540 NEXT I
